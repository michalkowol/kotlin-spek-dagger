group = 'pl.michalkowol'
version = '1.0.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1-M01'
    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M2'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'org.junit.platform.gradle.plugin'

defaultTasks 'run'

mainClassName = 'pl.michalkowol.BootKt'
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task fatJar(type: Jar) {
    baseName = "${project.name}-assembly"
    manifest = jar.manifest
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
    maven { url "http://dl.bintray.com/kotlin/kotlinx.coroutines" }
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'test/main/kotlin'
    main.java.srcDirs += 'src/main/kotlin'
}

junitPlatform {
    engines {
        include 'spek'
    }
}

kapt {
    generateStubs = true
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'ch.qos.logback:logback-classic:1.1.7'
    compile 'com.google.dagger:dagger:2.7'
    kapt 'com.google.dagger:dagger-compiler:2.7'
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'


    testCompile 'org.mockito:mockito-core:2.2.0-beta.1'
    testCompile 'org.jetbrains.spek:spek-api:1.0.89'
    testCompile 'org.jetbrains.kotlin:kotlin-test:1.0.4'
    testCompile 'org.junit.platform:junit-platform-runner:1.0.0-M2'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.0.89'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}